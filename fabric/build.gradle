plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group
dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:1.19"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.14.8"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.57.0+1.19"
}

sourceSets {
    main {
        java {
            srcDirs += ["../shared/src/main/java"]
        }
        resources {
            srcDirs += ["../shared/src/main/resources"]
        }
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}